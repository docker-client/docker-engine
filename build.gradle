buildscript {
    ext {
        kotlin_version = '1.1.3'
    }
    repositories {
        mavenLocal()
        jcenter()
        // testutil
        maven { url "http://dl.bintray.com/gesellix/docker-utils" }
        mavenCentral()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy:2.4.11'
    compile 'org.codehaus.groovy:groovy-json:2.4.11'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'

    compile 'com.squareup.okio:okio:1.13.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'

    compile 'org.apache.commons:commons-compress:1.14'

    compile 'de.gesellix:docker-filesocket:2017-06-24T14-21-18'
    testCompile 'de.gesellix:testutil:2017-06-25T08-04-50'

    compile 'org.bouncycastle:bcpkix-jdk15on:1.57'

    compile 'de.gesellix:docker-compose:2017-06-24T14-17-28'

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.5'

    testCompile 'commons-lang:commons-lang:2.6'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

test {
    // minimal way of providing a special environment variable for the EnvFileParserTest
    environment "A_KNOWN_VARIABLE", "my value"
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        engineClient(MavenPublication) {
            artifactId 'engine-client'
            version artifactVersion
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER ?: project["bintray.user"]
    key = System.env.BINTRAY_API_KEY ?: project["bintray.key"]
    publications = ['engineClient']
    pkg {
        repo = 'docker-utils'
        name = 'engine-client'
        desc = 'A Docker engine api client for the JVM'
        licenses = ['Apache-2.0']
        labels = ['docker', 'engine api', 'remote api', 'client', 'java']
        version = [
                name: artifactVersion
        ]
        vcsUrl = 'https://github.com/gesellix/docker-client.git'
    }
    dryRun = bintrayDryRun
}
bintrayUpload.dependsOn build
