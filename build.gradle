import java.text.SimpleDateFormat

buildscript {
    ext {
        kotlinVersion = "1.3.11"
    }

    repositories {
        mavenLocal()
        jcenter()
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
        classpath "gradle.plugin.net.ossindex:ossindex-gradle-plugin:0.1.1"
    }
}

ext {
    artifactVersion = new SimpleDateFormat("yyyy-MM-dd\'T\'HH-mm-ss").format(new Date())
}

def dependencyVersions = [
        "com.squareup.okio:okio:2.1.0",
        "org.codehaus.groovy:groovy:2.5.4",
        "org.codehaus.groovy:groovy-json:2.5.4",
        "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
]

subprojects {
    apply plugin: "groovy"
    apply plugin: "net.ossindex.audit"
    apply plugin: "com.github.ben-manes.versions"

    group = "de.gesellix"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
        }
    }
}

task updateWrapper(type: Wrapper) {
    gradleVersion = "5.0"
    distributionType = Wrapper.DistributionType.ALL
}
